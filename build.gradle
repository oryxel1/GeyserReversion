plugins {
    id 'java'
    id "com.gradleup.shadow" version "8.3.0"
    id "com.modrinth.minotaur" version "2.+"
}

group = 'oxy.geyser.reversion'
version = '1.0'

repositories {
    mavenCentral()
    maven {url = "https://repo.opencollab.dev/main/"}
}


dependencies {
    compileOnly("org.geysermc.geyser:core:2.9.0-SNAPSHOT") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    compileOnly("org.projectlombok:lombok:1.18.36")
    annotationProcessor("org.projectlombok:lombok:1.18.36")

    // Honestly it's not ideal to use an jar file directly, but eh this is convenient for me, and yes I know jitpack exist.
    implementation(files("libs/ouranos-1.0-all.jar"))
    implementation("com.fasterxml.jackson.core:jackson-annotations:2.17.0")
    implementation("com.fasterxml.jackson.core:jackson-annotations:2.17.0")
    implementation("com.fasterxml.jackson.core:jackson-databind:2.17.0")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17.0")

    implementation("net.lenni0451.classtransform:core:1.14.1")
    implementation("net.lenni0451:Reflect:1.5.0")
}

shadowJar {
    dependencies {
        include(dependency(files("libs/ouranos-1.0-all.jar")))
        include(dependency("com.fasterxml.jackson.core:jackson-databind:2.17.0"))
        include(dependency("com.fasterxml.jackson.core:jackson-annotations:2.17.0"))
        include(dependency("com.fasterxml.jackson.core:jackson-core:2.17.0"))
        include(dependency("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17.0"))

        include(dependency("net.lenni0451.classtransform:core:1.14.1"))
        include(dependency("net.lenni0451:Reflect:1.5.0"))

        include(dependency("org.ow2.asm:asm:9.8"))
        include(dependency("org.ow2.asm:asm-analysis:9.8"))
        include(dependency("org.ow2.asm:asm-commons:9.8"))
        include(dependency("org.ow2.asm:asm-tree:9.8"))
    }

    relocate("com.fasterxml.jackson", "oxy.geyser.reversion.shaded.jackson")
    relocate('org.objectweb.asm', 'oxy.geyser.reversion.shaded.asm')
    relocate('net.lenni0451.reflect', 'oxy.geyser.reversion.shaded.reflect')
    relocate('net.lenni0451.classtransform', 'oxy.geyser.reversion.shaded.classtransform')
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    versionName = version + "-" + getCommitHash()
    versionNumber = version + "-" + getCommitHash()
    changelog = getCommitMessage()
    projectId = "geyserreversion"
    versionType = "alpha"
    uploadFile = shadowJar
    // Don't comment on this :)
    gameVersions = ["1.8","1.8.1","1.8.2","1.8.3","1.8.4","1.8.5","1.8.6","1.8.7","1.8.8","1.8.9","1.9","1.9.1","1.9.2","1.9.3","1.9.4","1.10","1.10.1","1.10.2","1.11","1.11.1","1.11.2","1.12","1.12.1","1.12.2","1.13","1.13.1","1.13.2","1.14","1.14.1","1.14.2","1.14.3","1.14.4","1.15","1.15.1","1.15.2","1.16","1.16.1","1.16.2","1.16.3","1.16.4","1.16.5","1.17","1.17.1","1.18","1.18.1","1.18.2","1.19","1.19.1","1.19.2","1.19.3","1.19.4","1.20","1.20.1","1.20.2","1.20.3","1.20.4","1.20.5","1.20.6","1.21","1.21.1","1.21.2","1.21.3","1.21.4","1.21.5","1.21.6","1.21.7","1.21.8","1.21.9", "1.21.10"];
    loaders = ["geyser"]
}

def getCommitMessage() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'log', '-1', '--pretty=%B'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

// Thanks to https://gist.github.com/JonasGroeger/7620911 :tm:
def getCommitHash() {
    def gitFolder = "$projectDir/.git/"
    def takeFromHash = 7
    def head = new File(gitFolder + "HEAD").text.split(":")
    def isCommit = head.length == 1

    if(isCommit) return head[0].trim().take(takeFromHash)

    def refHead = new File(gitFolder + head[1].trim())
    refHead.text.trim().take takeFromHash
}